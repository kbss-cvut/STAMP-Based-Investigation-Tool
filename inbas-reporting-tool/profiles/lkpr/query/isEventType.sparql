  BASE <http://onto.fel.cvut.cz/ontologies/eccairs/aviation-3.4.0.2/>

  PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
  PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

  PREFIX eccairs-model: <http://onto.fel.cvut.cz/ontologies/eccairs/model/>
  PREFIX eccairs: <http://onto.fel.cvut.cz/ontologies/eccairs/>

  PREFIX eccairs-3.4.0.2: <http://onto.fel.cvut.cz/ontologies/eccairs/aviation-3.4.0.2/>

  PREFIX a: <http://onto.fel.cvut.cz/ontologies/aviation/core/>
  PREFIX ns1: <http://onto.fel.cvut.cz/ontologies/cz/lkpr/safety-database/vocab/>

  PREFIX aviation-safety: <http://onto.fel.cvut.cz/ontologies/aviation-safety/>
  PREFIX e: <>
  PREFIX r: <http://dev.inbas.cz/rdf4j-server/repositories/>

  ASK WHERE {
      {
        SERVICE r:eccairs-aviation-3.4.0.2 {
           # all events
           <a-390> eccairs-model:has-child+ ?term .
        }
      } UNION {
        SERVICE r:eccairs-aviation-3.4.0.2 {
           # all descriptive factors
           <a-385> eccairs-model:has-child+ ?term .
        }
      } UNION {
        SERVICE r:eccairs-aviation-3.4.0.2 {
           # all explanatory factors
           <a-392> eccairs-model:has-child+ ?term .
        }
      } UNION {
        ?term rdfs:subClassOf+ aviation-safety:safety_event .
      } UNION {
        ?term rdfs:subClassOf+ aviation-safety:Collision_Factors .
      } UNION {
        SERVICE r:cz-lkpr-safety-schema {
  	    ?term a [rdfs:subClassOf* a:safety-event-type] .
        }
      }
    }