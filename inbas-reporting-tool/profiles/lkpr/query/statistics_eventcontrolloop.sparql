PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

PREFIX aso: <http://onto.fel.cvut.cz/ontologies/aviation-safety/>
PREFIX ufo: <http://onto.fel.cvut.cz/ontologies/ufo/>
PREFIX doc: <http://onto.fel.cvut.cz/ontologies/documentation/>
PREFIX rt: <http://onto.fel.cvut.cz/ontologies/reporting-tool/>
PREFIX stamp: <http://onto.fel.cvut.cz/ontologies/stamp/>

PREFIX er: <http://dev.inbas.cz/rdf4j-server/repositories/>

SELECT ?deviation ?controlled_process ?controller (COUNT(DISTINCT ?et) AS ?count) WHERE {
  ?report doc:documents ?occurrence .

    FILTER NOT EXISTS {
      ?report <http://onto.fel.cvut.cz/ontologies/reporting-tool/model/has_next_revision> ?y .
    }

  ?occurrence ufo:has_part+ ?et .

  ?et a ufo:Event ;
    rt:has_event_type ?deviation_type .
  ?deviation_type a stamp:unsafe-event .

    OPTIONAL {
      ?deviation_type rdfs:label ?deviation .
    }
    OPTIONAL {
      SERVICE SILENT er:stamp {
        ?deviation_type rdfs:label ?deviation .
      }
    }
    OPTIONAL {
        SERVICE SILENT er:cast-stamp-schema-lkpr {
            ?activity_type stamp:has-control-structure-element-part ?deviation_type .
            ?process_type stamp:has-control-structure-element-part ?activity_type .
            OPTIONAL {
              ?process_type rdfs:label ?controlled_process .
            }
            OPTIONAL {
              SERVICE SILENT er:stamp {
                ?process_type rdfs:label ?controlled_process .
              }
            }
        }
    }
    OPTIONAL {
        SERVICE SILENT er:cast-stamp-schema-lkpr {
            ?activity_type ufo:has-participant ?controller_type .
            OPTIONAL {
              ?controller_type rdfs:label ?controller .
            }
            OPTIONAL {
              SERVICE SILENT er:stamp {
                ?controller_type rdfs:label ?controller .
              }
            }
        }
    }
}
GROUP BY ?deviation ?controlled_process ?controller
ORDER BY desc(?count) asc(?deviation)
