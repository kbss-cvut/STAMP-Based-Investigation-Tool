  BASE <http://onto.fel.cvut.cz/ontologies/eccairs-3.4.0.2/>

  PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
  PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

  PREFIX eccairs-model: <http://onto.fel.cvut.cz/ontologies/eccairs/model/>
  PREFIX eccairs: <http://onto.fel.cvut.cz/ontologies/eccairs/>

  PREFIX eccairs-3.4.0.2: <http://onto.fel.cvut.cz/ontologies/eccairs-3.4.0.2/>

  PREFIX a: <http://onto.fel.cvut.cz/ontologies/aviation/core/>
  PREFIX ns1: <http://onto.fel.cvut.cz/ontologies/cz/lkpr/safety-database/vocab/>

  PREFIX aviation-safety: <http://onto.fel.cvut.cz/ontologies/aviation-safety/>
  PREFIX e: <>
  PREFIX r: <http://martin.inbas.cz/openrdf-sesame/repositories/>

  CONSTRUCT {
    ?term rdfs:label ?label ;
    a ?type ;
    rdfs:comment ?comment ;
  } WHERE {
      {
        SERVICE r:eccairs-3.4.0.2-v-2016-02-23 {
           # all events
           <a-390> eccairs-model:has-child+  ?term.
           ?term rdfs:label ?label.
           ?term eccairs-model:has-explanation ?comment.
        }
        BIND(eccairs:event-type as ?type)
      } UNION {
        SERVICE r:eccairs-3.4.0.2-v-2016-02-23 {
           # all descriptive factors
           <a-385> eccairs-model:has-child+  ?term.
           ?term rdfs:label ?label.
           ?term eccairs-model:has-explanation ?comment.
        }
        BIND(eccairs:descriptive-factor as ?type)
      } UNION {
        SERVICE r:eccairs-3.4.0.2-v-2016-02-23 {
           # all explanatory factors
           <a-392> eccairs-model:has-child+  ?term.
           ?term rdfs:label ?label.
           ?term eccairs-model:has-explanation ?comment.
        }
        BIND(eccairs:explanatory-factor as ?type)
      } UNION {
        ?term rdfs:subClassOf+ aviation-safety:safety_event .
        # add all labels - not everything is returned due to missing labels
        ?term rdfs:label ?label.
        OPTIONAL {  ?term rdfs:comment ?comment. }
        BIND( aviation-safety:aviation-ontology-safety-event as ?type )
      } UNION {
        ?term rdfs:subClassOf+ aviation-safety:Collision_Factors.
        ?term rdfs:label ?label.
        OPTIONAL {  ?term rdfs:comment ?comment.}
        BIND(aviation-safety:Collision_Factors as ?type)
      } UNION {
        # REDA not working now
  #      ?term rdfs:subClassOf+ e:REDA_Contributing_Factors .
  #      ?term rdfs:label ?label.
  #      OPTIONAL {  ?term rdfs:comment ?comment.}
  #      BIND(e:REDA_Contributing_Factors as ?type)
      } UNION {
        SERVICE r:cz-lkpr-safety-schema {
  	    ?term a [rdfs:subClassOf* a:safety-event-type] ;
          ns1:Kategorie_KSP_Kategorie_KSP|ns1:Priciny_Pricina ?label
        }
      }
    }