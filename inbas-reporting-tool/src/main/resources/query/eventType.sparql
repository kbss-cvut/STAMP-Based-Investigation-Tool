#
# Generic query to get event types
#
BASE <http://onto.fel.cvut.cz/ontologies/aviation-safety/>

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

PREFIX eccairs-model: <http://onto.fel.cvut.cz/ontologies/eccairs/model/>
PREFIX eccairs: <http://onto.fel.cvut.cz/ontologies/eccairs/>

PREFIX eccairs-current: <http://onto.fel.cvut.cz/ontologies/eccairs/aviation-3.4.0.2/>

CONSTRUCT {
  ?term rdfs:label ?label ;
  a ?type ;
  rdfs:comment ?comment ;
} WHERE {
    {
      SERVICE <http://martin.inbas.cz/openrdf-sesame/repositories/eccairs-aviation-3.4.0.2> {
         # all events
         eccairs-current:a-390 eccairs-model:has-child+  ?term.
         ?term rdfs:label ?label.
         ?term eccairs-model:has-explanation ?comment.
      }
      BIND(eccairs:event-type as ?type)
    } UNION {
      SERVICE <http://martin.inbas.cz/openrdf-sesame/repositories/eccairs-aviation-3.4.0.2> {
         # all descriptive factors
         eccairs-current:a-385 eccairs-model:has-child+  ?term.
         ?term rdfs:label ?label.
         ?term eccairs-model:has-explanation ?comment.
      }
      BIND(eccairs:descriptive-factor as ?type)
    } UNION {
      SERVICE <http://martin.inbas.cz/openrdf-sesame/repositories/eccairs-aviation-3.4.0.2> {
         # all explanatory factors
         eccairs-current:a-392 eccairs-model:has-child+  ?term.
         ?term rdfs:label ?label.
         ?term eccairs-model:has-explanation ?comment.
      }
      BIND(eccairs:explanatory-factor as ?type)
    } UNION {
      ?term rdfs:subClassOf+ <safety_event> .
      # add all labels - not everything is returned due to missing labels
      ?term rdfs:label ?label.
      OPTIONAL {  ?term rdfs:comment ?comment.}
      BIND(<safety-event> as ?type)
  }
}
