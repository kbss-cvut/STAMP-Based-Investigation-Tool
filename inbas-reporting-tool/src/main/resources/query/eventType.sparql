BASE <http://onto.fel.cvut.cz/ontologies/eccairs-3.4.0.2/>

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX eccairs-model: <http://onto.fel.cvut.cz/ontologies/eccairs/model/>
PREFIX eccairs: <http://onto.fel.cvut.cz/ontologies/eccairs/>

CONSTRUCT {    
	?term rdfs:label ?label ;        
	a ?type ;        
	rdfs:comment ?comment ;
} WHERE {
{
	# all events
	# j.0:A-390 j.0:hasChild ?c.
	# ANS events
	<a-390> eccairs-model:has-child+ ?term.
	BIND(eccairs:event-type as ?type)
} UNION {
	# Aircraft operation DF
	<vl-v4-cd-descriptive-factors/v-10000000> eccairs-model:has-child+ ?term.
	BIND(eccairs:descriptive-factor as ?type)
} UNION {
	# ATM DF
	<vl-v4-cd-descriptive-factors/v-20000000> eccairs-model:has-child+ ?term.
	BIND(eccairs:descriptive-factor as ?type)
} UNION {
	# METEO DF
	<vl-v4-cd-descriptive-factors/v-50000000> eccairs-model:has-child+ ?term.
	BIND(eccairs:descriptive-factor as ?type)
}
	?term rdfs:label ?label.
	?term eccairs-model:has-explanation ?comment.
}
