PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

PREFIX ufo: <http://onto.fel.cvut.cz/ontologies/ufo/>
PREFIX doc: <http://onto.fel.cvut.cz/ontologies/documentation/>
PREFIX aso: <http://onto.fel.cvut.cz/ontologies/aviation-safety/>
PREFIX rt: <http://onto.fel.cvut.cz/ontologies/reporting-tool/>
PREFIX stamp: <http://onto.fel.cvut.cz/ontologies/stamp/>

PREFIX er: <http://dev.inbas.cz/rdf4j-server/repositories/>

SELECT ?event_type ?event_type_iri ?year ?month (COUNT(*) AS ?count) {
  ?report doc:documents ?occurrence .

  FILTER NOT EXISTS {
    ?report <http://onto.fel.cvut.cz/ontologies/reporting-tool/model/has_next_revision> ?y .
  }

  ?occurrence stamp:contains-loss-event-of-type ?event_type_iri ;
              aso:has_start_time ?start_time .

  BIND(year(?start_time) AS ?year)
  BIND(month(?start_time) AS ?month)

  OPTIONAL {
    SERVICE SILENT er:eccairs-aviation-3.4.0.2 {
      ?event_type_iri rdfs:label ?event_type.
    }
  }
#  OPTIONAL {
#    SERVICE SILENT er:eccairs-aviation-1.3.0.8 {
#      ?event_type_iri rdfs:label ?event_type.
#    }
#  }
  OPTIONAL {
    SERVICE SILENT er:inbas-model-2016-01-21:22-46-17 {
      ?event_type_iri rdfs:label ?event_type.
    }
  }
  OPTIONAL {
    SERVICE SILENT ?eventTypesRepository {
      ?event_type_iri rdfs:label ?event_type.
    }
  }
}
GROUP BY ?year ?month ?event_type ?event_type_iri
ORDER BY desc(?count) ?year ?month asc(?event_type)
