PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

PREFIX aso: <http://onto.fel.cvut.cz/ontologies/aviation-safety/>
PREFIX ufo: <http://onto.fel.cvut.cz/ontologies/ufo/>
PREFIX doc: <http://onto.fel.cvut.cz/ontologies/documentation/>
PREFIX rt: <http://onto.fel.cvut.cz/ontologies/reporting-tool/>
PREFIX stamp: <http://onto.fel.cvut.cz/ontologies/stamp/>

PREFIX er: <http://dev.inbas.cz/rdf4j-server/repositories/>

SELECT ?deviation ?controlled_process ?controller (COUNT(DISTINCT ?et) AS ?count) WHERE {
  ?report doc:documents ?occurrence .

    FILTER NOT EXISTS {
      ?report <http://onto.fel.cvut.cz/ontologies/reporting-tool/model/has_next_revision> ?y .
    }

  ?occurrence ufo:has_part+ ?et .

  ?et a ufo:Event ;
    rt:has_event_type ?deviation_type .

  ?at ufo:has_part ?et .
  ?pt ufo:has_part ?at .
  ?at rt:has_event_type ?activity_type.
  ?pt rt:has_event_type ?process_type.

  SERVICE SILENT <http://localhost:8080/rdf4j-server/repositories/sbit-fd-schema> {
    ?deviation_type a stamp:unsafe-event .
    ?process_type stamp:has-control-structure-element-part ?activity_type.
    ?activity_type ufo:has-participant ?controller_type.
    ?deviation_type rdfs:label ?deviation .
    ?controller_type rdfs:label ?controller .
    ?process_type rdfs:label ?controlled_process .
  }

}
GROUP BY ?deviation ?controlled_process ?controller
ORDER BY desc(?count) asc(?deviation)
